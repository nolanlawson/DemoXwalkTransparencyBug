apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    maven {
        url 'https://download.01.org/crosswalk/releases/crosswalk/android/maven2'
    }
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "com.nolanlawson.demo"
        minSdkVersion 18
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        withCrosswalkArmv7 {
            ndk {
                abiFilter "armeabi-v7a"
            }
            versionCode ((100 * computeVersionCode()) + 2)
        }
        withCrosswalkX86 {
            ndk {
                abiFilter "x86"
            }
            versionCode ((100 * computeVersionCode()) + 4)
        }
        withoutCrosswalk {
            versionCode ((100 * computeVersionCode()) + 6)
        }
    }

    sourceSets {
        withCrosswalkArmv7.java.srcDirs = ['src/withCrosswalk/java']
        withCrosswalkX86.java.srcDirs = ['src/withCrosswalk/java']
    }
}

def computeVersionCode() {
    return android.defaultConfig.versionCode
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:21.0.3'

    withCrosswalkArmv7Compile 'org.xwalk:xwalk_core_library_beta:11.40.277.1'
    withCrosswalkX86Compile 'org.xwalk:xwalk_core_library_beta:11.40.277.1'
}
